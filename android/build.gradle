group = "me.junjie.xing.flutter_aria2"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.2.20"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.11.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "me.junjie.xing.flutter_aria2"

    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        debug.jniLibs.srcDirs += "${buildDir}/generated/jniLibs/debug"
        release.jniLibs.srcDirs += "${buildDir}/generated/jniLibs/release"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 24
        ndk {
            abiFilters "arm64-v8a"
        }
    }

    externalNativeBuild {
        cmake {
            path = file("CMakeLists.txt")
        }
    }

    dependencies {
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

tasks.register("prepareAria2DebugJniLibs", Copy) {
    from("aria2lib/Debug/lib/libaria2_c_api.so")
    into("${buildDir}/generated/jniLibs/debug/arm64-v8a")
}

tasks.register("prepareAria2ReleaseJniLibs", Copy) {
    from("aria2lib/Release/lib/libaria2_c_api.so")
    into("${buildDir}/generated/jniLibs/release/arm64-v8a")
}

tasks.named("preBuild") {
    dependsOn("prepareAria2DebugJniLibs", "prepareAria2ReleaseJniLibs")
}
